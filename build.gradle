/*
 * Copyright (c) 2021 tecc
 * Lahc is licensed under the MIT licence.
 */

plugins {
    id 'java-library'
    id 'maven-publish'
}

group 'me.tecc.lahc'
version '0.1.0-alpha'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.jetbrains:annotations:22.0.0'

    compileOnly 'org.projectlombok:lombok:1.18.20'
    testCompileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.20'

    implementation 'org.slf4j:slf4j-api:2.0.0-alpha1'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.2'

    testImplementation 'ch.qos.logback:logback-classic:1.3.0-alpha5'
}

test {
    useJUnitPlatform()
}

task expandSources(type: Copy) {
    from 'src/main/java'
    into 'build/adjustedSrc'

    eachFile { FileCopyDetails fcd ->
        if (fcd.relativePath.lastName == "Lahc.java") {
            expand(project: project)
        }
    }
    outputs.upToDateWhen { false }
}
if (hasProperty("release")) {
    compileJava.dependsOn expandSources
    compileJava.source = expandSources.destinationDir
}

publishing {
    repositories {
        maven {
            name 'snapshots'
            url 'https://m2.tecc.me/snapshots'
            credentials {
                username System.getenv("LACH_PUBLISH_USERNAME")
                password System.getenv("LACH_PUBLISH_PASSWORD")
            }
        }
    }
    publications {
        if (project.hasProperty("release")) {
            println 'Enabling release publication'
            release(MavenPublication) {
                from components.java
            }
        }
    }
}